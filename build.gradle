buildscript {
  repositories {
    jcenter()
  }
  dependencies {
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '2.0.2'
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.4.1'
}

defaultTasks 'dist'

ext {
  publishedProjects = subprojects - project(':solr-hadoop-common')
}

subprojects {

  if (project in publishedProjects) {

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'project-report'
    apply plugin: 'jacoco'

    gradle.projectsEvaluated {
      tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
      }
    }

    repositories {
      mavenLocal()
      jcenter()
      mavenCentral()
    }

    project.configurations {
      hadoop2Compile.extendsFrom(project.configurations.compile)
      hadoop2Runtime.extendsFrom(project.configurations.runtime, hadoop2Compile)
      hadoop2TestCompile.extendsFrom(project.configurations.testCompile)
      hadoop2TestRuntime.extendsFrom(project.configurations.testRuntime)

      hadoop3Compile.extendsFrom(project.configurations.compile)
      hadoop3Runtime.extendsFrom(project.configurations.runtime, hadoop3Compile)
      hadoop3TestCompile.extendsFrom(project.configurations.testCompile)
      hadoop3TestRuntime.extendsFrom(project.configurations.testRuntime)
      // Ensure that "project()" deps elsewhere still a copy of the hadoop classes
      it."default".extendsFrom(project.configurations.hadoop2Compile)
    }

    compileJava {
      classpath = configurations.hadoop3Compile
    }

    compileTestJava {
      classpath = configurations.hadoop3TestCompile + sourceSets.main.output
    }
    
    test {
      classpath = configurations.hadoop3TestRuntime + sourceSets.main.output + sourceSets.test.output
      reports.html.destination = file("$buildDir/reports/tests")
      reports.junitXml.destination = file("$buildDir/test-results")
    }

    jacocoTestReport {
      reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
      }
    }

    publishing {
      publications {
        core(MavenPublication) {
          from components.java
        }
      }
    }

    task dist(dependsOn: "publishToMavenLocal") {
    }
  }

}

project('solr-hive-core') {

  dependencies {
    compile(project(':solr-hadoop-common:solr-hadoop-io')) {
      transitive = false
    }
    compile("org.apache.hive:hive-common:${hiveVersion}") {
      transitive = false
    }
    compile("org.apache.hive:hive-exec:${hiveVersion}") {
      transitive = false
    }
    compile("org.apache.solr:solr-solrj:${solrVersion}") {
      exclude group: 'org.apache.hadoop'
      exclude group: 'junit'
    }
    compile("org.apache.hadoop:hadoop-common:${hadoop3Version}@jar")
    compile("org.apache.hadoop:hadoop-client:${hadoop3Version}") {
      exclude group: 'log4j'
      exclude group: 'org.slf4j'
    }

    compile("org.apache.hadoop:hadoop-auth:${hadoop3Version}") {
      transitive = false
    }
    compile("org.apache.hadoop:hadoop-mapreduce-client-core:${hadoop3Version}") {
      transitive = false
    }
    compile("org.apache.hadoop:hadoop-hdfs:${hadoop3Version}@jar") {
      transitive = false
    }

    testCompile 'com.carrotsearch.randomizedtesting:junit4-ant:1.4.0'
    testCompile "org.apache.lucene:lucene-analyzers-common:${solrVersion}"
    testCompile "commons-collections:commons-collections:3.2.1"

    testCompile ("org.apache.solr:solr-test-framework:${solrVersion}") {
      exclude group: 'junit'
    }
    testCompile "junit:junit:4.12"
    testCompile (project(':solr-hadoop-common:solr-hadoop-testbase')) {
      transitive = false
    }
    testCompile(project(':solr-hadoop-common:solr-hadoop-document')) {
      transitive = false
    }

    // The com.klarna:hiverunner project doesn't currently support Hive 3, so we temporarily
    // rely on a custom HiveRunner fork to run tests against a Hive 3 instance.  The code for this
    // dep can be found here: https://github.com/gerlowskija/HiveRunner/tree/hive_3_0_0
    testCompile files('libs/hiverunner-hive3-0.0.1.jar')
  }
}


project('solr-hive-serde') {
  apply plugin: 'com.github.johnrengelman.shadow'

  shadowJar {
    zip64 true
    mergeServiceFiles()
    classifier = ''
    dependencies {
      exclude(dependency("org.apache.hadoop:.*"))
      exclude(dependency("org.apache.hive:.*"))
    }

    relocate 'org.apache.http', 'shaded.org.apache.http'
    relocate 'org.apache.solr', 'shaded.org.apache.solr'
    // Shade guava due Tez conflicts
    relocate 'com.google.common', 'shaded.com.google.common'
    // Remove unwanted files in root jar (repackaging)
    exclude 'JDOMAbout*.class'
    exclude '.gitkeep'
    exclude '*.txt'
    exclude '*.dtd'
    exclude '*.gif'
    exclude '*.properties'
    exclude '*.xml'
    exclude '*.ico'
    exclude '*.lst'
    exclude '*.css'
    exclude 'NOTICE'
    exclude 'JHIGHLIGHT_VERSION'
    exclude 'LICENSE'
  }
  dependencies {
    compile(project(':solr-hive-core'))

    compile(project(':solr-hadoop-common:solr-hadoop-document')) {
      transitive = false
    }
  }

  publishing {
    publications {
      shadow(MavenPublication) {
        project.shadow.component(it)
      }
    }
  }
}

